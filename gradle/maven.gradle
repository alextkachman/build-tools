/*
 * Copyright 2012 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// custom maven tasks

task sourceJar(type: Jar) {
	description = 'Builds a source jar artifact suitable for maven deployment.'
	classifier = 'sources'
	from sourceSets.main.java, sourceSets.main.groovy
}

task javadocJar(type: Jar) {
	description = 'Builds a groovydoc jar artifact suitable for maven deployment.'
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task groovydocJar(type: Jar) {
	description = 'Builds a groovydoc jar artifact suitable for maven deployment.'
	classifier = 'groovydoc'
	from groovydoc.destinationDir
}
groovydocJar.dependsOn groovydoc

build.dependsOn sourceJar, javadocJar, groovydocJar

artifacts {
	archives sourceJar, javadocJar, groovydocJar
}

if (!hasProperty('sonatypeUsername')) {
	ext.sonatypeUsername = ''
}
if (!hasProperty('sonatypePassword')) {
	ext.sonatypePassword = ''
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// maven task configuration

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
	required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
	sign configurations.archives
}

uploadArchives {
	group 'build'
	description = "Does a maven deploy of archives artifacts"

	repositories {
		mavenDeployer {
			configuration = configurations.archives
			if (isReleaseVersion) {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			}
			name = 'mavenCentralReleaseDeployer'
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
				releases(updatePolicy: 'always')
				snapshots(updatePolicy: 'always')
			}
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
				releases(updatePolicy: 'always')
				snapshots(updatePolicy: 'always')
			}
			configurePom(pom)
		}
	}
}
uploadArchives.dependsOn build

install {
	group = 'build'
	description = "Does a maven install of archives artifacts to local m2 cache"
	configurePom(repositories.mavenInstaller.pom)
}
install.dependsOn build


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// configuration methods

def configurePom(def pom) {
	pom.project {
		name project.name
		description 'vert.x - effortless polyglot'
		groupId project.group
		packaging 'jar'
		url 'http://vertx.io'
		inceptionYear '2011'
		
		developers {
            developer {
                id 'purplefox'
                name 'Tim Fox'
                email 'tim@tfox.org'
            }
            developer {
                id 'pidster'
                name 'Pidster'
                email 'pid@pidster.com'
            }
        }

		scm {
	        connection 'scm:https://vert-x@github.com/vert-x/vert.x-build-tools'
	        developerConnection 'scm:git@github.com:vert-x/vert.x-build-tools.git'
	        url 'https://github.com/vert-x/vertx-build-tools'
		}

		properties {
			setProperty('project.build.sourceEncoding', 'UTF8')
		}
	}
}

